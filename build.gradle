apply plugin: 'java'
apply plugin: 'idea'

compileJava {
    sourceCompatibility '1.8'
    targetCompatibility '1.8'
}

sourceSets {
    main.java.srcDirs 'src'
}

ext {
    api_version = '0.1.2'
    api_filename = 'rd-api.jar';
    mod_jar_filename = 'rd-mod-time-slower.jar'
    lwjgl_version = 'lwjgl-2.9.3'
    lwjgl_filename = "${project.ext.lwjgl_version}.zip"
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly files(
            "libs/${project.ext.lwjgl_version}/jar/lwjgl.jar",
            "libs/${project.ext.lwjgl_version}/jar/lwjgl_util.jar",
    )
    compileOnly fileTree( dir: "libs", includes: ['*.jar'] )
}

task downloadLwjgl(type: FileDownloader) {
    sourceUrl = "https://www.dropbox.com/scl/fi/jkko3vw0h35kitxpm7pai/lwjgl-2.9.3.zip?rlkey=477rw7zj62fi7k1q1rnkg7zap&st=oefgpib6&dl=1"
    target = new File( "libs/${project.ext.lwjgl_filename}" )
}

task extractLwjgl(type: Copy, dependsOn: [ 'downloadLwjgl' ]) {
    from zipTree( "libs/${project.ext.lwjgl_filename}" )
    into 'libs'
}

task downloadAPI(type: FileDownloader) {
    sourceUrl = "https://github.com/martinambrus/RDApi/releases/download/${project.ext.api_version}/rd-api.jar"
    target = new File( "libs/${project.ext.api_filename}" )
}

task first_time_run( dependsOn: [ 'extractLwjgl', 'downloadAPI' ] ) {}

jar {
    archiveFileName = project.ext.mod_jar_filename
}

class FileDownloader extends DefaultTask {
    @Input
    String sourceUrl

    @OutputFile
    File target

    @TaskAction
    void download() {
        ant.get(src: sourceUrl, dest: target)
    }
}